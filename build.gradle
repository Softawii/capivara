plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.7.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.softwaii.capivara'

version '1.0.3.0'


repositories {
    mavenCentral()
    maven { url 'https://jitpack.io/' }
}

application {
    mainClass = 'com.softawii.capivara.Main'
}

springBoot {
    buildInfo()
}

def urlFile = { url, name ->
    File file = new File("$buildDir/download/${name}.jar")
    file.parentFile.mkdirs()
    if (!file.exists()) {
        new URL(url).withInputStream { downloadStream ->
            file.withOutputStream { fileOut ->
                fileOut << downloadStream
            }
        }
    }
    files(file.absolutePath)
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.0'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    implementation 'com.h2database:h2:2.1.212'
    implementation 'org.postgresql:postgresql:42.3.6'

    implementation("net.dv8tion:JDA:5.0.0-alpha.11") {
        exclude module: 'opus-java'
    }

    implementation urlFile('https://github.com/Softawii/curupira/releases/download/v0.1.2.1/curupira-0.1.2.1.jar', 'curupira-0.1.2.1')

    implementation("com.github.minndevelopment:emoji-java:master-SNAPSHOT")
}

test {
    useJUnitPlatform()
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17